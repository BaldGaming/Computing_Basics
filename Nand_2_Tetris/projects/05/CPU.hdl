// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM == 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to restart the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // Address of next instruction

    PARTS:
    
    Mux16 (a = instruction, b = ALUout, sel = instruction[15] , out = Aout); // Mux for the A register;

    // These two create the load for the ARegister;
    Not (in = instruction[15], out = NotCinst);
    Or (a = NotCinst, b = instruction[5], out = LoadA);

    ARegister (in = Aout, load = LoadA, out = outAReg, out[0..14] = addressM); // ARegister;

    Mux16 (a = outAReg, b = inM, sel = instruction[12], out = yOut); // Mux for ALU y input;

    And (a = instruction[4], b = instruction[15], out = LoadD); // load for DRegister

    DRegister (in = ALUout, load = LoadD, out = outDReg); // DRegister

    ALU (x = outDReg,
         y = yOut,
         zx = instruction[11],
         nx = instruction[10],
         zy = instruction[9],
         ny = instruction[8],
         f = instruction[7],
         no = instruction[6],
         
         out = ALUout, out = outM, zr = zr, ng = ng); // ALU

    And (a = instruction[3], b = instruction[15], out = writeM); // writeM.

    PC (in = outAReg, inc = true, load = PCload, reset = reset, out[0..14] = pc); // pc

    // JMP (Unconditional)
    And(a = instruction[2], b = instruction[1], out = JMP1);
    And(a = JMP1, b = instruction[0], out = JMP);

    // JEQ (Equal 0)
    And(a = instruction[1], b = zr, out = JEQ);

    // JNE (Not Equal 0)
    Not (in = zr, out = notZr);
    And (a = instruction[2], b = instruction[0], out = JNE1);
    And (a = JNE1, b = notZr, out = JNE);

    // JGT (Greater Than 0)
    Not (in = ng, out = notNg);
    And(a = instruction[0], b = notNg, out = JGT1);
    And(a = JGT1, b = notZr, out = JGT);

    // JLT (Less Than 0)
    And(a = instruction[2], b = ng, out = JLT);

    // JGE (Greater or Equal 0)
    And(a = instruction[1], b = instruction[0], out = JGE1);
    And(a = JGE1, b = notNg, out = JGE);

    // JLE (Less or Equal 0)
    And(a = instruction[2], b = instruction[1], out = JLE1);
    Or(a = ng, b = zr, out = isNegOrZero);
    And(a = JLE1, b = isNegOrZero, out = JLE);

    Or8Way (in[0] = JMP,
            in[1] = JEQ,
            in[2] = JNE,
            in[3] = JGT,
            in[4] = JLT,
            in[5] = JGE,
            in[6] = JLE,
            in[7] = false,
            out = jmpFlag);

    And (a = instruction[15], b = jmpFlag, out = PCload);

}
